def projectName = 'thirdfoot-rto'
def langLevel = 1.7
def tomcatVersion = '7.0.11'

allprojects {
  apply plugin: 'eclipse'
 
  group = 'net.thirdfoot.rto'
  version = '0.1'
}

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.2.3'
  }
}
 
subprojects {
  apply plugin: 'java'
 
  sourceCompatibility = langLevel
  targetCompatibility = langLevel
 
  repositories {
    mavenLocal()
    mavenCentral()

    maven {
      url 'http://xuggle.googlecode.com/svn/trunk/repo/share/java/'
    }
  }
 
  dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.powermock:powermock-api-mockito:1.5.4'
    testCompile 'org.powermock:powermock-module-junit4:1.5.4'
    testCompile 'org.springframework:spring-context:3.2.5.RELEASE'
    testCompile 'org.springframework:spring-test:3.2.5.RELEASE'
  }
}

project(':rto-kernel') {
  dependencies {
    compile 'org.jodd:jodd-http:3.5.1'
    compile 'org.jodd:jodd-props:3.5.1'
    compile 'org.mortbay.jetty:servlet-api:3.0.20100224'
    compile 'org.python:jython:2.7-b1'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'xuggle:xuggle-xuggler:5.4'

    runtime 'ch.qos.logback:logback-classic:1.1.2'
  }
}
 
project(':rto-service') {
  dependencies {
    compile project(':rto-kernel')

    compile 'org.hibernate:hibernate-core:4.1.7.Final'
  }
}

project(':rto-web') {
  apply plugin: 'war'
  apply plugin: 'tomcat'

  war {
    baseName = projectName
  }
 
  dependencies {
    compile project(':rto-service')

    compile 'org.springframework:spring-webmvc:3.2.5.RELEASE'

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
      "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
      exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
  }
}
 
eclipse {
  project {
    jdkName = langLevel
    languageLevel = langLevel
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
}